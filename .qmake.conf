
# This file contains the global definitions and the bootstrapping
# for the whole project build process
#
# Only modify if you are 100% sure what you are doing


# All source files are in the SOURCE tree,
# the intermediate build files are in the BUILD tree
# and the final product goes to the DEPLOY tree
#
# Since it is highly advisable to use a shadow build configuration,
# we should not mess with the build directory at all, thats qmake's domain.
SOURCE_ROOT_PWD = $$PWD
BUILD_ROOT_PWD  = $$shadowed($$PWD)
DEPLOY_ROOT_PWD = $$PWD/bin
CONFIG_DIR      = $$SOURCE_ROOT_PWD/config

# --- Functions required for easier bootstrapping ---
# Determines the path to the requested config file and returns it for use with
# include(...)
# Should the config file not exist, an error is produced and the build is
# aborted.
#
# Example:
# include($$getConfigFile(config_foo.pri))

defineReplace(getConfigFile){

    FILE = $$CONFIG_DIR/$$1
    !exists($$FILE): error(Missing config file $$FILE)
    return($$FILE)
}

# --- Bootstrapping utility functions and paths that are required globally---

include($$getConfigFile(config_functions.pri))
include($$getConfigFile(config_paths.pri))
